plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.devsoap.vaadin-flow' version "1.4"
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
    id 'com.palantir.docker' version '0.25.0'
}

group = "com.faendir.om.sekt"
version = "1.0.0"

vaadin {
    compatibilityMode = true
    version = "14.1.21"
}

repositories {
    mavenCentral()
    maven { setUrl("https://dl.bintray.com/f43nd1r/maven") }
    vaadin.repositories()
    mavenLocal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.71")
    implementation("com.faendir.om:dsl:1.1.4")
    implementation vaadin.bom()
    implementation vaadin.core()
    implementation vaadin.dependency('spring-boot-starter')
    implementation('com.juicy:juicyaceeditor:1.0.9')
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}
tasks {
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

task unpackBootJar(type: Copy) {
    from zipTree(bootJar.outputs.files[0])
    into file("$buildDir/dependency")
    dependsOn bootJar
}

docker {
    name = "f43nd1r/omsekt:latest"
    files unpackBootJar.outputs, "java.policy"
    dependsOn unpackBootJar
}
